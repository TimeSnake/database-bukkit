plugins {
  id 'java'
  id 'java-base'
  id 'java-library'
  id 'maven-publish'
  id("io.papermc.paperweight.userdev") version "1.7.2"
  id("xyz.jpenilla.run-paper") version "2.1.0"
  id('com.github.johnrengelman.shadow') version "8.1.1"
}


group = "de.timesnake"
version = '2.7.0'
var projectId = 44

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url "https://repo.papermc.io/repository/maven-public/"
  }
  maven {
    url "https://git.timesnake.de/api/v4/groups/7/-/packages/maven"
    name "TimeSnake"
    credentials {
      username = timesnakeUser
      password = timesnakePassword
    }
  }
}

dependencies {
  implementation "de.timesnake:database-api:2.+"

  compileOnly "com.moandjiezana.toml:toml4j:0.7.3-SNAPSHOT"

  paperweight.paperDevBundle("1.21-R0.1-SNAPSHOT")
}

configurations.configureEach {
  resolutionStrategy.dependencySubstitution {
    if (project.parent != null) {
      substitute module("de.timesnake:database-api") using project(":database-api")
    }
  }
}



tasks.register('exportAsPlugin', Copy) {
  from "$buildDir/libs/${archivesBaseName}-${project.version}.jar"
  into "${timesnakePluginDir}"

  dependsOn(assemble)
}

publishing {
  repositories {
    maven {
      url "https://git.timesnake.de/api/v4/projects/" + projectId + "/packages/maven"
      credentials(HttpHeaderCredentials) {
        name = "Job-Token"
        value = System.getenv("CI_JOB_TOKEN")
      }
      authentication {
        header(HttpHeaderAuthentication)
      }
    }
  }

  publications {
    mavenJava(MavenPublication) {
      from components.java

      artifact "$buildDir/libs/${archivesBaseName}-${project.version}.jar"
    }
  }
}

tasks.withType(JavaCompile).configureEach {
  options.encoding = 'UTF-8'
}

java {
  withSourcesJar()
}

paperweight.reobfArtifactConfiguration = io.papermc.paperweight.userdev.ReobfArtifactConfiguration.getMOJANG_PRODUCTION()